apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'signing'

def artifactName = 'xxhash-android'
group = project.rootProject.group
version = project.rootProject.version

android {
  compileSdkVersion androidCompileSdkVersion
  buildToolsVersion androidBuildToolsVersion

  defaultConfig {
    minSdkVersion androidMinSdkVersion
    targetSdkVersion androidTargetSdkVersion
    versionCode 1
    versionName version
    archivesBaseName = 'xxhash-android'
    testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    externalNativeBuild {
      cmake {
        cFlags "-std=c99", "-fvisibility=hidden", "-I.", "-I${project.file("external/xxHash")}"
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      externalNativeBuild {
        cmake {
          cFlags "-O2", "-g0"
        }
      }
    }
  }

  externalNativeBuild {
    cmake {
      version "3.10.2"
      path "CMakeLists.txt"
    }
  }
}

dependencies {
  implementation "com.google.code.findbugs:jsr305:$jsr305Version"

  testImplementation "junit:junit:$junitVersion"

  androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
  androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoVersion"
  androidTestImplementation "androidx.test:runner:$androidxTestRunnerVersion"
  androidTestImplementation "androidx.test.ext:junit:$androidxTestExtJunitVersion"
}

android.libraryVariants.all { variant ->
  tasks.addRule("Publishing configuration for ${variant.name} variant") { taskName ->
    if (taskName == "javadoc${variant.name.capitalize()}") {
      tasks.create(taskName, Javadoc) {
        source = variant.sourceSets.javaDirectories
        classpath += project.files(variant.javaCompileProvider.map { it.classpath })
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

        if (JavaVersion.current().isJava8Compatible()) {
          options.addStringOption('Xdoclint:none', '-quiet')
          options.encoding "UTF-8"
        }
      }
    } else if (taskName == "javadocJar${variant.name.capitalize()}") {
      tasks.create(taskName, Jar) {
        archiveClassifier.set('javadoc')
        def javadoc = tasks["javadoc${variant.name.capitalize()}"]
        from javadoc.destinationDir
        dependsOn javadoc
      }
    } else if (taskName == "sourcesJar${variant.name.capitalize()}") {
      tasks.create(taskName, Jar) {
        archiveClassifier.set('sources')
        from variant.sourceSets.javaDirectories
      }
    }
  }
}

publishing {
  android.libraryVariants.all { variant ->
    publications {
      create(variant.name.capitalize(), MavenPublication) {
        artifactId artifactName
        variant.outputs.each { output ->
          artifact output.packageLibrary
        }

        artifact tasks["javadocJar${variant.name.capitalize()}"]
        artifact tasks["sourcesJar${variant.name.capitalize()}"]

        pom {
          name = 'xxhash-android'
          description = 'An implementation of xxhash for Android.'
          inceptionYear = '2017'
          url = 'https://github.com/joomcode/xxhash-android'
          packaging = 'aar'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution = 'repo'
            }
          }
          organization {
            name = 'Joom'
            url = 'https://joom.com'
          }
          developers {
            developer {
              id = 'MichaelRocks'
              name = 'Michael Rozumyanskiy'
              email = 'michael.rozumyanskiy@gmail.com'
            }
          }
          scm {
            connection = 'scm:git:git://github.com/joomcode/xxhash-android.git'
            developerConnection = 'scm:git:ssh://git@github.com/joomcode/xxhash-android.git'
            url = 'https://github.com/joomcode/xxhash-android'
          }
        }

        def dependencies = project.configurations["${variant.baseName}CompileClasspath"].allDependencies
        if (!dependencies.empty) {
          pom.withXml {
            asNode().appendNode("dependencies").with {
              dependencies.each { dependency ->
                appendNode("dependency").with {
                  appendNode("groupId", dependency.group)
                  appendNode("artifactId", dependency.name)
                  appendNode("version", dependency.version)
                }
              }
            }
          }
        }
      }
    }
  }

  repositories {
    if (project.hasProperty('mavenCentralRepositoryUsername') && project.hasProperty('mavenCentralRepositoryPassword')) {
      maven {
        name 'Sonatype'
        url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
        credentials {
          username mavenCentralRepositoryUsername
          password mavenCentralRepositoryPassword
        }
      }
    }
  }
}

signing {
  android.libraryVariants.all { variant ->
    sign publishing.publications.getByName(variant.name.capitalize())
  }
}
