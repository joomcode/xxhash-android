buildscript {
  version = '1.2.0'
  group = 'com.joom'

  ext.androidCompileSdkVersion = 30
  ext.androidBuildToolsVersion = "30.0.3"
  ext.androidMinSdkVersion = 16
  ext.androidTargetSdkVersion = 30

  ext.androidToolsVersion = '4.2.0-beta06'
  ext.kotlinVersion = '1.4.32'

  ext.jsr305Version = '3.0.2'

  ext.junitVersion = '4.13.2'
  ext.androidxEspressoVersion = '3.3.0'
  ext.androidxTestRunnerVersion = '1.3.0'
  ext.androidxTestExtJunitVersion = '1.1.2'

  repositories {
    google()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$androidToolsVersion"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

allprojects { project ->
  def publishPropertiesFile = rootProject.file('publish.properties')
  if (publishPropertiesFile.exists()) {
    def localProperties = new Properties()
    publishPropertiesFile.withInputStream { stream -> localProperties.load(stream) }
    localProperties.each { property ->
      if (property.key == "signing.secretKeyRingFile") {
        // If this is the key ring, treat it as a relative path
        project.ext.set(property.key, rootProject.file(property.value).absolutePath)
      } else {
        project.ext.set(property.key, property.value)
      }
    }
  }
}
